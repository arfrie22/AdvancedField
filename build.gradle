plugins {
    id 'java'
    id 'java-library'
    id 'org.openjfx.javafxplugin' version '0.0.12'
}

group 'com.github.arfrie22'
version '1.0'
sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

repositories {
    mavenCentral()
    maven {
        name = "WPILib"
        setUrl("https://frcmaven.wpi.edu/artifactory/release")
    }
}

javafx {
    modules = [ 'javafx.fxml' ]
}

dependencies {
    compileOnly group: "edu.wpi.first.shuffleboard", name: "api", version: "2020.3.2"
    compileOnly group: "edu.wpi.first.shuffleboard.plugin", name: "networktables", version: "2020.3.2"


    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
}

test {
    useJUnitPlatform()
}

tasks.register("installPlugin", Copy) {
    from(tasks.named("jar"))
    into("${System.getProperty("user.home")}/Shuffleboard/plugins")
    description = "Builds the plugin JAR and installs it in the Shuffleboard plugins directory."
    group = "Shuffleboard Plugin"
}